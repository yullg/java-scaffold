java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'scaffold'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'java-scaffold'
                description = 'The java scaffold project.'
                url = 'https://github.com/yullg/java-scaffold'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'yullg'
                        name = 'yullg'
                        email = 'yullg@foxmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/yullg/java-scaffold.git'
                    developerConnection = 'scm:git:ssh://github.com/yullg/java-scaffold.git'
                    url = 'https://github.com/yullg/java-scaffold'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
	            username = ossrhUsername
	            password = ossrhPassword
	        }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}